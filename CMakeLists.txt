CMAKE_MINIMUM_REQUIRED(VERSION 3.13)

SET(CMAKE_MODULE_PATH "${CMAKE_SOURCE_DIR}/cmake/" ${CMAKE_MODULE_PATH})

IF(POLICY CMP0074)
	CMAKE_POLICY(SET CMP0074 NEW)
ENDIF()

PROJECT(EQEmu)

IF(NOT CMAKE_BUILD_TYPE)
	SET(CMAKE_BUILD_TYPE RelWithDebInfo CACHE STRING "Choose the type of build." FORCE)
ENDIF(NOT CMAKE_BUILD_TYPE)

SET(CMAKE_CXX_STANDARD 14)
SET(CMAKE_CXX_STANDARD_REQUIRED ON)
SET(CMAKE_CXX_EXTENSIONS OFF)

LIST(APPEND GCC_IGNORE -Wno-format-overflow)
LIST(APPEND GCC_IGNORE -Wno-address-of-packed-member)
LIST(APPEND GCC_IGNORE -Wno-stringop-overflow)
LIST(APPEND GCC_IGNORE -Wno-format-truncation)
LIST(APPEND GCC_IGNORE -Wno-unused-but-set-variable)
LIST(APPEND GCC_IGNORE -Wno-sign-compare)
LIST(APPEND GCC_IGNORE -Wno-cast-function-type)

ADD_COMPILE_OPTIONS("$<$<CXX_COMPILER_ID:GNU>:${GCC_IGNORE}>")

ENABLE_TESTING()
FIND_PACKAGE(GTest REQUIRED)
INCLUDE(GoogleTest)

IF(MSVC)
	IF(CMAKE_CL_64)
		SET(BOOST_ROOT "${CMAKE_CURRENT_SOURCE_DIR}/dependencies/boost")
		SET(ZLIB_ROOT "${CMAKE_CURRENT_SOURCE_DIR}/dependencies/zlib_x64")
		SET(MYSQL_ROOT "${CMAKE_CURRENT_SOURCE_DIR}/dependencies/mysql_x64")
		SET(LUA_ROOT "${CMAKE_CURRENT_SOURCE_DIR}/dependencies/luaj_x64")
		SET(CURL_ROOT "${CMAKE_CURRENT_SOURCE_DIR}/dependencies/curl_x64")
	ELSE(CMAKE_CL_64)
		SET(BOOST_ROOT "${CMAKE_CURRENT_SOURCE_DIR}/dependencies/boost")
		SET(ZLIB_ROOT "${CMAKE_CURRENT_SOURCE_DIR}/dependencies/zlib_x86")
		SET(MYSQL_ROOT "${CMAKE_CURRENT_SOURCE_DIR}/dependencies/mysql_x86")
		SET(LUA_ROOT "${CMAKE_CURRENT_SOURCE_DIR}/dependencies/luaj_x86")
		SET(CURL_ROOT "${CMAKE_CURRENT_SOURCE_DIR}/dependencies/curl_x86")
	ENDIF(CMAKE_CL_64)

	ADD_DEFINITIONS(-D_CRT_SECURE_NO_WARNINGS)
	ADD_DEFINITIONS(-DNOMINMAX)
	ADD_DEFINITIONS(-DCRASH_LOGGING)

	SET(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} /MP")
ELSE(MSVC)
	ADD_DEFINITIONS(-DHAS_UNION_SEMUN)
ENDIF(MSVC)

#FreeBSD support
IF(UNIX)
	IF(CMAKE_SYSTEM_NAME MATCHES "FreeBSD")
		ADD_DEFINITIONS(-DFREEBSD)
		ADD_DEFINITIONS(-D_GLIBCXX_USE_C99)
		SET(FREEBSD TRUE)
	ENDIF(CMAKE_SYSTEM_NAME MATCHES "FreeBSD")
	IF(CMAKE_SYSTEM_NAME MATCHES "Darwin")
		ADD_DEFINITIONS(-DDARWIN)
		SET(DARWIN TRUE)
	ENDIF(CMAKE_SYSTEM_NAME MATCHES "Darwin")
ENDIF(UNIX)

ADD_DEFINITIONS(-DGLM_FORCE_RADIANS)
ADD_DEFINITIONS(-DGLM_FORCE_CTOR_INIT)
ADD_DEFINITIONS(-DGLM_ENABLE_EXPERIMENTAL)

#MSVC can fetch dependencies automatically.
#IF(MSVC)
#	INCLUDE("${CMAKE_SOURCE_DIR}/cmake/DependencyHelperMSVC.cmake")
#ENDIF()

#Find everything we need
FIND_PACKAGE(Boost REQUIRED)
FIND_PACKAGE(MySQL)
FIND_PACKAGE(MariaDB)
FIND_PACKAGE(ZLIB)
FIND_PACKAGE(OpenSSL)
FIND_PACKAGE(Lua51)
FIND_PACKAGE(mbedTLS)
FIND_PACKAGE(CURL REQUIRED)
FIND_PACKAGE(yaml-cpp REQUIRED)

MESSAGE(STATUS "**************************************************")
MESSAGE(STATUS "* Library Detection                              *")
MESSAGE(STATUS "**************************************************")

IF(MYSQL_FOUND)
	MESSAGE(STATUS "* MySQL:                                   FOUND *")
ELSE()
	MESSAGE(STATUS "* MySQL:                                 MISSING *")
ENDIF()

IF(MARIADB_FOUND)
	MESSAGE(STATUS "* MariaDB:                                 FOUND *")
ELSE()
	MESSAGE(STATUS "* MariaDB:                               MISSING *")
ENDIF()

IF(ZLIB_FOUND)
	MESSAGE(STATUS "* ZLIB:                                    FOUND *")
ELSE()
	MESSAGE(STATUS "* ZLIB:                                  MISSING *")
ENDIF()

IF(Lua51_FOUND)
	MESSAGE(STATUS "* Lua:                                     FOUND *")
ELSE()
	MESSAGE(STATUS "* Lua:                                   MISSING *")
ENDIF()

IF(OpenSSL_FOUND)
	MESSAGE(STATUS "* OpenSSL:                                 FOUND *")
ELSE()
	MESSAGE(STATUS "* OpenSSL:                               MISSING *")
ENDIF()

IF(MBEDTLS_FOUND)
	MESSAGE(STATUS "* mbedTLS:                                 FOUND *")
ELSE()
	MESSAGE(STATUS "* mbedTLS:                               MISSING *")
ENDIF()

MESSAGE(STATUS "**************************************************")

#options
OPTION(EQEMU_DEPOP_INVALIDATES_CACHE "#repop invalidates the npc_types cache (will cause a larger database hit on #repop but is more convienent)." ON)
OPTION(EQEMU_COMMANDS_LOGGING "Enable GM Command logs" ON)
OPTION(EQEMU_BUILD_SERVER "Build the game server." ON)
OPTION(EQEMU_BUILD_LOGIN "Build the login server." ON)

#PRNG options
OPTION(EQEMU_ADDITIVE_LFIB_PRNG "Use Additive LFib for PRNG." OFF)
MARK_AS_ADVANCED(EQEMU_ADDITIVE_LFIB_PRNG)
OPTION(EQEMU_BIASED_INT_DIST "Use biased int dist instead of uniform." OFF)
MARK_AS_ADVANCED(EQEMU_BIASED_INT_DIST)
SET(EQEMU_CUSTOM_PRNG_ENGINE "" CACHE STRING "Custom random engine. (ex. std::default_random_engine)")
MARK_AS_ADVANCED(EQEMU_CUSTOM_PRNG_ENGINE)

IF(CMAKE_COMPILER_IS_GNUCXX)
	OPTION(EQEMU_SFMT19937 "Use GCC's extention for SIMD Fast MT19937." OFF)
	MARK_AS_ADVANCED(EQEMU_SFMT19937)
ENDIF()

IF(EQEMU_ADDITIVE_LFIB_PRNG)
	ADD_DEFINITIONS(-DUSE_ADDITIVE_LFIB_PRNG)
	IF(EQEMU_SFMT19937)
		MESSAGE(STATUS "SFMT19937 and ADDITITVE_LFIB_PRNG both set, SFMT19937 ignored.")
		SET(EQEMU_SFMT19937 OFF)
	ENDIF()
	IF(NOT EQEMU_CUSTOM_PRNG_ENGINE STREQUAL "")
		MESSAGE(STATUS "CUSTOM_PRNG_ENGINE and ADDITITVE_LFIB_PRNG both set, CUSTOM_PRNG_ENGINE ignored.")
		SET(EQEMU_CUSTOM_PRNG_ENGINE "")
	ENDIF()
ENDIF()

IF(EQEMU_SFMT19937)
	ADD_DEFINITIONS(-DUSE_SFMT19937)
	IF(NOT EQEMU_CUSTOM_PRNG_ENGINE STREQUAL "")
		MESSAGE(STATUS "CUSTOM_PRNG_ENGINE and SFMT19937 both set, CUSTOM_PRNG_ENGINE ignored.")
		SET(EQEMU_CUSTOM_PRNG_ENGINE "")
	ENDIF()
ENDIF()

IF(NOT EQEMU_CUSTOM_PRNG_ENGINE STREQUAL "")
	ADD_DEFINITIONS(-DUSE_CUSTOM_PRNG_ENGINE=${EQEMU_CUSTOM_PRNG_ENGINE})
ENDIF()

IF(EQEMU_BIASED_INT_DIST)
	ADD_DEFINITIONS(-DBIASED_INT_DIST)
ENDIF()


IF(EQEMU_COMMANDS_LOGGING)
	ADD_DEFINITIONS(-DCOMMANDS_LOGGING)
ENDIF(EQEMU_COMMANDS_LOGGING)

#database
IF(MySQL_FOUND AND MariaDB_FOUND)
	SET(DATABASE_LIBRARY_SELECTION MariaDB CACHE STRING "Database library to use:
			MySQL
			MariaDB"
	)

	IF(DATABASE_LIBRARY_SELECTION STREQUAL "MySQL")
		SET(DATABASE_LIBRARY_TYPE "  MySQL")
		SET(DATABASE_LIBRARY_LIBS ${MySQL_LIBRARIES})
		SET(DATABASE_LIBRARY_INCLUDE ${MySQL_INCLUDE_DIR})
	ELSEIF(DATABASE_LIBRARY_SELECTION STREQUAL "MariaDB")
		SET(DATABASE_LIBRARY_TYPE "MariaDB")
		SET(DATABASE_LIBRARY_LIBS ${MariaDB_LIBRARIES})
		SET(DATABASE_LIBRARY_INCLUDE ${MariaDB_INCLUDE_DIR})
	ELSE()
		MESSAGE(FATAL_ERROR "Unknown database library set, should be one of: MySQL, MariaDB")
	ENDIF()
ELSEIF(MariaDB_FOUND)
	SET(DATABASE_LIBRARY_TYPE "MariaDB")
	SET(DATABASE_LIBRARY_LIBS ${MariaDB_LIBRARIES})
	SET(DATABASE_LIBRARY_INCLUDE ${MariaDB_INCLUDE_DIR})
ELSEIF(MySQL_FOUND)
	SET(DATABASE_LIBRARY_TYPE "  MySQL")
	SET(DATABASE_LIBRARY_LIBS ${MySQL_LIBRARIES})
	SET(DATABASE_LIBRARY_INCLUDE ${MySQL_INCLUDE_DIR})
ELSE()
	MESSAGE(FATAL_ERROR "One of MySQL or MariaDB is a required dependency.")
ENDIF()

#security
#prefer openssl to mbedtls (arbitrary)
IF(OpenSSL_FOUND AND MBEDTLS_FOUND)
	SET(TLS_LIBRARY_SELECTION OpenSSL CACHE STRING "TLS library to use:
			OpenSSL
			mbedTLS"
	)

	IF(TLS_LIBRARY_SELECTION STREQUAL "OpenSSL")
		SET(TLS_LIBRARY_TYPE " OpenSSL")
		SET(TLS_LIBRARY_ENABLED ON)
		SET(TLS_LIBRARY_LIBS ${OPENSSL_LIBRARIES})
		SET(TLS_LIBRARY_INCLUDE ${OPENSSL_INCLUDE_DIR})
		ADD_DEFINITIONS(-DEQEMU_USE_OPENSSL)
	ELSEIF(TLS_LIBRARY_SELECTION STREQUAL "mbedTLS")
		SET(TLS_LIBRARY_TYPE " mbedTLS")
		SET(TLS_LIBRARY_ENABLED ON)
		SET(TLS_LIBRARY_LIBS ${MBEDTLS_LIBRARY} ${MBEDX509_LIBRARY} ${MBEDCRYPTO_LIBRARY})
		SET(TLS_LIBRARY_INCLUDE ${MBEDTLS_INCLUDE_DIR})
		ADD_DEFINITIONS(-DEQEMU_USE_MBEDTLS)
	ELSE()
		MESSAGE(FATAL_ERROR "Unknown TLS library set, should be one of: OpenSSL, mbedTLS")
	ENDIF()
ELSEIF(OpenSSL_FOUND)
	SET(TLS_LIBRARY_TYPE " OpenSSL")
	SET(TLS_LIBRARY_ENABLED ON)
	SET(TLS_LIBRARY_LIBS ${OPENSSL_LIBRARIES})
	SET(TLS_LIBRARY_INCLUDE ${OPENSSL_INCLUDE_DIR})
	ADD_DEFINITIONS(-DEQEMU_USE_OPENSSL)
ELSEIF(MBEDTLS_FOUND)
	SET(TLS_LIBRARY_TYPE " mbedTLS")
	SET(TLS_LIBRARY_ENABLED ON)
	SET(TLS_LIBRARY_LIBS ${MBEDTLS_LIBRARY} ${MBEDX509_LIBRARY} ${MBEDCRYPTO_LIBRARY})
	SET(TLS_LIBRARY_INCLUDE ${MBEDTLS_INCLUDE_DIR})
	ADD_DEFINITIONS(-DEQEMU_USE_MBEDTLS)
ELSE()
	SET(TLS_LIBRARY_TYPE "Disabled")
	SET(TLS_LIBRARY_ENABLED OFF)
ENDIF()

IF(Lua51_FOUND)
	SET(LUA_LIBRARY_TYPE " Lua 5.1")
	SET(LUA_LIBRARY_ENABLED ON)
	SET(LUA_LIBRARY_LIBS ${LUA_LIBRARY} luabind)
	SET(LUA_LIBRARY_INCLUDE ${LUA_INCLUDE_DIR} "${CMAKE_CURRENT_SOURCE_DIR}/libs/luabind")
ELSE()
	SET(LUA_LIBRARY_TYPE "Disabled")
	SET(LUA_LIBRARY_ENABLED OFF)
ENDIF()

#use zlib if exists
IF(ZLIB_FOUND)
	OPTION(EQEMU_BUILD_ZLIB "Build internal version of zlib." ON)
	IF(EQEMU_BUILD_ZLIB)
		SET(ZLIB_LIBRARY_TYPE "zlib-ng")
		SET(ZLIB_LIBRARY_LIBS "zlibstatic")
		SET(ZLIB_LIBRARY_INCLUDE "${CMAKE_CURRENT_SOURCE_DIR}/libs/zlibng")
		INCLUDE_DIRECTORIES(SYSTEM "${CMAKE_CURRENT_BINARY_DIR}/libs/zlibng")
	ELSE()
		SET(ZLIB_LIBRARY_TYPE "   zlib")
		SET(ZLIB_LIBRARY_LIBS ${ZLIB_LIBRARY})
		SET(ZLIB_LIBRARY_INCLUDE ${ZLIB_INCLUDE_DIRS})
	ENDIF()
ELSE()
	SET(ZLIB_LIBRARY_TYPE "zlib-ng")
	SET(ZLIB_LIBRARY_LIBS "zlibstatic")
	SET(ZLIB_LIBRARY_INCLUDE "${CMAKE_CURRENT_SOURCE_DIR}/libs/zlibng")
ENDIF()

MESSAGE(STATUS "")
MESSAGE(STATUS "**************************************************")
MESSAGE(STATUS "* Library Usage                                  *")
MESSAGE(STATUS "**************************************************")
MESSAGE(STATUS "* Database:                              ${DATABASE_LIBRARY_TYPE} *")
MESSAGE(STATUS "* TLS:                                  ${TLS_LIBRARY_TYPE} *")
MESSAGE(STATUS "* Lua:                                  ${LUA_LIBRARY_TYPE} *")
MESSAGE(STATUS "* zlib:                                  ${ZLIB_LIBRARY_TYPE} *")
MESSAGE(STATUS "**************************************************")

#setup server libs and headers
SET(SERVER_LIBS common ${DATABASE_LIBRARY_LIBS} ${ZLIB_LIBRARY_LIBS} ${Boost_LIBRARIES} uv_a fmt RecastNavigation::Detour ${CURL_LIBRARY} ${YAML_CPP_LIBRARIES})

INCLUDE_DIRECTORIES(SYSTEM "${DATABASE_LIBRARY_INCLUDE}")
INCLUDE_DIRECTORIES(SYSTEM "${ZLIB_LIBRARY_INCLUDE}")
INCLUDE_DIRECTORIES(SYSTEM "${Boost_INCLUDE_DIRS}")
INCLUDE_DIRECTORIES(SYSTEM "${CMAKE_CURRENT_SOURCE_DIR}/submodules/glm")
INCLUDE_DIRECTORIES(SYSTEM "${CMAKE_CURRENT_SOURCE_DIR}/submodules/cereal/include")
INCLUDE_DIRECTORIES(SYSTEM "${CMAKE_CURRENT_SOURCE_DIR}/submodules/fmt/include")
INCLUDE_DIRECTORIES(SYSTEM "${CMAKE_CURRENT_SOURCE_DIR}/submodules/libuv/include" )
INCLUDE_DIRECTORIES(SYSTEM "${CMAKE_CURRENT_SOURCE_DIR}/submodules/recastnavigation/DebugUtils/Include")
INCLUDE_DIRECTORIES(SYSTEM "${CMAKE_CURRENT_SOURCE_DIR}/submodules/recastnavigation/Detour/Include")
INCLUDE_DIRECTORIES(SYSTEM "${CMAKE_CURRENT_SOURCE_DIR}/submodules/recastnavigation/DetourCrowd/Include")
INCLUDE_DIRECTORIES(SYSTEM "${CMAKE_CURRENT_SOURCE_DIR}/submodules/recastnavigation/DetourTileCache/Include")
INCLUDE_DIRECTORIES(SYSTEM "${CMAKE_CURRENT_SOURCE_DIR}/submodules/recastnavigation/Recast/Include")
INCLUDE_DIRECTORIES(SYSTEM "${CMAKE_CURRENT_SOURCE_DIR}/submodules/websocketpp")
INCLUDE_DIRECTORIES(${YAML_INCLUDE_DIRS})

OPTION(EQEMU_BUILD_LOGGING "Build Logging (To speed up compilation)" ON)
IF(EQEMU_BUILD_LOGGING)
	ADD_DEFINITIONS(-DBUILD_LOGGING)
ENDIF()

IF(TLS_LIBRARY_ENABLED)
	SET(SERVER_LIBS ${SERVER_LIBS} ${TLS_LIBRARY_LIBS})
	INCLUDE_DIRECTORIES(SYSTEM "${TLS_LIBRARY_INCLUDE}")
ENDIF()

IF(LUA_LIBRARY_ENABLED)
	OPTION(EQEMU_BUILD_LUA "Build Lua parser." ON)

	IF(EQEMU_BUILD_LUA)
		ADD_DEFINITIONS(-DLUA_EQEMU)
		SET(SERVER_LIBS ${SERVER_LIBS} ${LUA_LIBRARY_LIBS})
		INCLUDE_DIRECTORIES(SYSTEM "${LUA_LIBRARY_INCLUDE}")

		OPTION(EQEMU_SANITIZE_LUA_LIBS "Sanitize Lua Libraries (Remove OS and IO standard libraries from being able to run)." ON)
		IF(EQEMU_SANITIZE_LUA_LIBS)
			ADD_DEFINITIONS(-DSANITIZE_LUA_LIBS)
		ENDIF()
	ENDIF()
ENDIF()

IF(CURL_FOUND)
	INCLUDE_DIRECTORIES(SYSTEM "${CURL_INCLUDE_DIR}")
ENDIF()

IF(WIN32)
	SET(SERVER_LIBS ${SERVER_LIBS} "ws2_32" "psapi" "iphlpapi" "userenv")
ENDIF()

IF(UNIX)
	SET(SERVER_LIBS ${SERVER_LIBS} ${CMAKE_DL_LIBS} "z" "m" "pthread")
	IF(NOT DARWIN)
		SET(SERVER_LIBS ${SERVER_LIBS} "rt")
	ENDIF()
	# Freebsd provides uuids in the C library
	IF(NOT ${CMAKE_SYSTEM_NAME} STREQUAL "FreeBSD")
		SET(SERVER_LIBS ${SERVER_LIBS} "uuid")
	ENDIF()
ENDIF()

IF(EQEMU_BUILD_LOGIN AND NOT TLS_LIBRARY_ENABLED)
	MESSAGE(FATAL_ERROR "Login server requires a TLS Library to build.")
ENDIF()

IF(EQEMU_BUILD_SERVER OR EQEMU_BUILD_LOGIN)
	ADD_SUBDIRECTORY(common)
	ADD_SUBDIRECTORY(libs)
	ADD_SUBDIRECTORY(submodules/fmt)
	ADD_SUBDIRECTORY(submodules/libuv)

	IF(EQEMU_BUILD_ZLIB)
		SET(ZLIB_COMPAT ON CACHE BOOL "Compile with zlib compatible API")
		SET(ZLIB_ENABLE_TESTS OFF CACHE BOOL "Build test binaries")
		ADD_SUBDIRECTORY(libs/zlibng)
	ENDIF()

	SET(RECASTNAVIGATION_DEMO OFF CACHE BOOL "Build demo")
	SET(RECASTNAVIGATION_TESTS OFF CACHE BOOL "Build tests")
	SET(RECASTNAVIGATION_EXAMPLES OFF CACHE BOOL "Build examples")
	ADD_SUBDIRECTORY(submodules/recastnavigation)
ENDIF(EQEMU_BUILD_SERVER OR EQEMU_BUILD_LOGIN)

IF(EQEMU_BUILD_SERVER)
	ADD_SUBDIRECTORY(shared_memory)
	ADD_SUBDIRECTORY(world)
	ADD_SUBDIRECTORY(zone)
	ADD_SUBDIRECTORY(ucs)
	ADD_SUBDIRECTORY(queryserv)
ENDIF(EQEMU_BUILD_SERVER)

IF(EQEMU_BUILD_LOGIN)
	ADD_SUBDIRECTORY(loginserver)
ENDIF(EQEMU_BUILD_LOGIN)